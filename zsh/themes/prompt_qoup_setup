# Load dependencies.

pmodload 'helper'

function prompt_qoup_async_git {
  cd -q "$1"
  if (( $+functions[git-info] )); then
    git-info
    print ${git_info[status]}
  fi
}

function prompt_qoup_async_tasks {
  # Initialize async worker. This needs to be done here and not in
  # prompt_qoup_setup so the git formatting can be overridden by other prompts.
  if (( !${prompt_qoup_async_init:-0} )); then
    async_start_worker prompt_qoup -n
    async_register_callback prompt_qoup prompt_qoup_async_callback
    typeset -g prompt_qoup_async_init=1
  fi

  # Kill the old process of slow commands if it is still running.
  async_flush_jobs prompt_qoup

  # Compute slow commands in the background.
  async_job prompt_qoup prompt_qoup_async_git "$PWD"
}

function prompt_qoup_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_qoup_pwd=$(prompt-pwd)

  # Handle updating git data. We also clear the git prompt data if we're in a
  # different git root now.
  if (( $+functions[git-dir] )); then
    local new_git_root="$(git-dir 2> /dev/null)"
    if [[ $new_git_root != $_qoup_cur_git_root ]]; then
      _prompt_qoup_git=''
      _qoup_cur_git_root=$new_git_root
    fi
  fi

  # Run python info (this should be fast and not require any async)
  if (( $+functions[python-info] )); then
    python-info
  fi

  prompt_qoup_async_tasks
}

function prompt_qoup_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)
  _prompt_qoup_precmd_async_pid=0
  _prompt_qoup_precmd_async_data=$(mktemp "${TMPDIR:-/tmp}/qoup-prompt-async-XXXXXXXXXX")

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz async && async

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_qoup_precmd

  local -A symbols colors
  zstyle -a ':prezto:module:prompt:qoup' symbols symbols
  zstyle -a ':prezto:module:prompt:qoup' colors colors

  local prompt_symbol=${symbols[prompt]:-'%#'}
  local prompt_color=${colors[prompt]:-white}
  local prmpt="%(!. %B%F{1}${prompt_symbol}%f%b.%B%F{$prompt_color}${prompt_symbol}%f%b)"

  # Set editor-info parameters.
#  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'
  zstyle ':prezto:module:prompt' pwd-length 'long'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action'    format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added'     format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead'     format ' %%B%F{13}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind'    format ' %%B%F{13}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch'    format ' %%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit'    format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted'   format ' %%B%F{1}✖%f%%b'
  zstyle ':prezto:module:git:info:modified'  format ' %%B%F{4}✱%f%%b'
  zstyle ':prezto:module:git:info:position'  format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed'   format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed'   format ' %%B%F{6}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged'  format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}◼%f%%b'
  zstyle ':prezto:module:git:info:keys'      format 'status' '%b %p %c:%s%A%B%S%a%d%m%r%U%u'

  # Default is to show the return value
  if zstyle -T ':prezto:module:prompt' show-return-val; then
    local show_return="%? ${symbols[return]:-↵}"
  else
  	local show_return="✘ "
  fi

  # Set python-info format
  zstyle ':prezto:module:python:info:virtualenv' format '%f%F{3}(%v)%F{7} '

  # Get the async worker set up
  _qoup_cur_git_root=''

  _prompt_qoup_git=''
  _prompt_qoup_pwd=''

  # Define prompts.
  PROMPT='╭─${SSH_TTY:+"%F{9}%n%f%F{7}@%f%F{3}%m%f  "} %F{4}${_prompt_qoup_pwd}%f  ${_prompt_qoup_git}'$'\n'"╰─${prmpt}"'${editor_info[keymap]} '
  RPROMPT='$python_info[virtualenv]${editor_info[overwrite]}%(?:: %F{1}'
  RPROMPT+=${show_return}
  RPROMPT+='%f)'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_qoup_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'qoup'
}

function prompt_qoup_async_callback {
  case $1 in
    prompt_qoup_async_git)
      # We can safely split on ':' because it isn't allowed in ref names.
      IFS=':' read _git_target _git_post_target <<< "$3"

      # The target actually contains 3 space separated possibilities, so we need to
      # make sure we grab the first one.
      _git_target=$(coalesce ${(@)${(z)_git_target}})

      if [[ -z "$_git_target" ]]; then
        # No git target detected, flush the git fragment and redisplay the prompt.
        if [[ -n "$_prompt_qoup_git" ]]; then
          _prompt_qoup_git=''
          zle && zle reset-prompt
        fi
      else
        # Git target detected, update the git fragment and redisplay the prompt.
        _prompt_qoup_git="${_git_target}${_git_post_target}"
        zle && zle reset-prompt
      fi
      ;;
  esac
}

prompt_qoup_setup "$@"
