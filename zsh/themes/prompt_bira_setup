function +vi-prompt_bira_set_message {
  	local branch="%B%F{magenta}${hook_com[branch]}%b"
  	local staged unstaged
  	
    [ $oh_my_git_info[behind] -gt 0 ] && branch+="%{↓%G%}$oh_my_git_info[behind]"	# commits behind
    [ $oh_my_git_info[ahead]  -gt 0 ] && branch+="%{↑%G%}$oh_my_git_info[ahead]"	# commits ahead
    
    branch+="%f"

    [ $oh_my_git_info[staged] -gt 0 ] && staged="%F{118}●$oh_my_git_info[staged]%f"

	if [ $oh_my_git_info[unstaged] -gt 0 ]; then
		local changed=$(( $oh_my_git_info[modifications] + $oh_my_git_info[deletions] ))

#		[ $oh_my_git_info[conflicts] -gt 0 ] && unstaged+="%F{red}%{✖%G%}$oh_my_git_info[conflicts]%f"
		[ $oh_my_git_info[conflicts] -gt 0 ] && unstaged+="%F{red}✘$oh_my_git_info[conflicts]%f"
		[ $changed                   -gt 0 ] && unstaged+="%F{135}±$changed%f"
#		[ $oh_my_git_info[untracked] -gt 0 ] && unstaged+="%F{124}%{…%G%}$oh_my_git_info[untracked]%f"
		[ $oh_my_git_info[untracked] -gt 0 ] && unstaged+="%F{244}?$oh_my_git_info[untracked]%f"
	else
		unstaged="%{$fg_bold[green]%}%{✔%G%}%f"
	fi

	hook_com[branch]=$branch
	hook_com[staged]=$staged
	hook_com[unstaged]=$unstaged

	[ -n $hook_com[action] ] && hook_com[action]="%F{118}${oh_my_git_info[action]}%f"
}

function prompt_bira_precmd() {
	vcs_info 'prompt'
}

function prompt_bira_setup() {
	autoload -Uz add-zsh-hook vcs_info

	#local return_code='%(?..%F{red}%? ↵%f)'
#	local return_code='%(?..%F{red}%? %f)'
	local return_code='%(?..%F{red}%? ↵%f)'
	local user_host='%F{118}%n@%m%f'
	local current_dir='%B%F{blue}%~%f%b'
	local prompt_symbol

	case "$OSTYPE" in
		darwin*) prompt_symbol="" ;;
		cygwin*) prompt_symbol="" ;;
		linux*) prompt_symbol="" ;;
		*) prompt_symbol="•" ;;
	esac

	# Add hook for calling vcs_info before each command.
	add-zsh-hook precmd prompt_bira_precmd

	# Set vcs_info parameters.
	zstyle ':vcs_info:*' enable git
	zstyle ':vcs_info:*:prompt:*' check-for-changes true
	zstyle ':vcs_info:*:prompt:*' formats "(%b|%c%u)"
	zstyle ':vcs_info:*:prompt:*' actionformats "(%b|%c%u|%a)"
	zstyle ':vcs_info:git*+set-message:*' hooks prompt_bira_set_message

	PROMPT="╭─${user_host}  ${current_dir}  "'${vcs_info_msg_0_}'"
╰─%B${prompt_symbol}%b "
	RPS1="${return_code}"
}

prompt_bira_setup "$@"
